# Paquetes

Agrupa Procedimientos y Funciones

Esta formado por:
* Package Specification
* Package Body

> Un Package Body no puede existir sin un Package Specification

> El Specification y el Body deben tener el mismo nombre

## Visibilidad dentro de paquetes

* Publico: Package Specification - Visible por otros paquetes/SELECT/etc
* Privado: Package Body - Visible solo desde dentro del paquete

## Objetos de un paquete

### Package Specification

Permite declarar:

* Types
* Cusores
* Variables
* Constantes
* Header de Procedimientos y Funciones

Todo lo declarado dentro del Specification es publico.

### Package Body

Permite declarar:

* Types
* Cursores
* Variables
* Constantes
* Procedimientos y Funciones (codigo)

Todo lo declarado dentro del Body es privado
(excepto los procedimientos y funciones previamente declarados en el Specification).

## Sobrecarga

Permite definir N procedimientos o funciones que tengan el mismo
nombre pero que tengan diferente cantidad o tipo de parametros.

## Forward Declaration

Hacer una declaracion del header de los procedimientos o funciones
que estan a nivel privado, para evitar tener en cuenta el orden de
implementacion de las mismas y evitar errores de compilacion.

## One Time Only

Cargar tablas en memoria a nivel de paquete.

Cuando se invoca una funcion en un paquete, y el paquete es
cargado por primera vez. El paquete carga 1 o N tablas en memoria
para utilizar dentro del paquete.

> Siempre va en el final del Body

## Ejemplo de un Paquete

Specification:

```sql
CREATE OR REPLACE PACKAGE NOMBRE_PAQUETE IS
  v_nro Number; -- variable

  c_iva CONSTANT Number(2) := 21; -- constante

  CURSOR nombre_cursor IS
    SELECT * FROM employee; -- cursor

  PROCEDURE NOMBRE_PROCEDIMIENTO(p_nro Number);

  PROCEDURE NOMBRE_PROCEDIMIENTO(p_nro VARCHAR2);

  FUNCTION GET_NRO(p_id customer.customer_id%TYPE) RETURN customer.phone_number%TYPE;

  PROCEDURE MUESTRA;

  -- One Time Only (Declaracion de tabla)
  TYPE reg IS RECORD(
               j_id job.job_id%TYPE,
               nom job.function%TYPE,
               cant Number);
  TYPE tab IS TABLE OF reg
    INDEX BY BINARY_INTEGER;

  v_tab tab;
END;
```

Body:

```sql
CREATE OR REPLACE PACKAGE BODY NOMBRE_PAQUETE IS
  v_nro2 Number := 1000; -- variable privada

  PROCEDURE NOMBRE_PROCEDIMIENTO(p_nro Number) IS
    v_total Number;
  BEGIN
    v_total := p_nro * c_iva;

    DBMS_OUTPUT.put_line(v_total);
  END;

  PROCEDURE NOMBRE_PROCEDIMIENTO(p_nro VARCHAR2) IS
    v_total Number;
  BEGIN
    v_total := TO_NUMBER(p_nro) * c_iva;

    DBMS_OUTPUT.put_line(v_total);
  END;

  FUNCTION GET_NRO(p_id customer.customer_id%TYPE)
  RETURN customer.phone_number%TYPE
  IS
    v_tel customer.phone_number%TYPE;
  BEGIN
    SELECT phone_number INTO v_tel
    FROM customer
    WHERE customer_id = p_id;

    RETURN v_tel;
  EXCEPTION
    WHEN NO_DATA_FOUND THEN
      RAISE_APPLICATION_ERROR(-20100, 'No existe el cliente para el id: ' || p_id);
  END;

  PROCEDURE MUESTRA IS
  BEGIN
    FOR i IN v_tab.FIRST..v_tab.LAST LOOP
      DBMS_OUTPUT.PUT_LINE(v_tab(i).j_id || ', ' || v_tab(i).nom || ', ' || v_tab(i).cant);
    END LOOP;
  END;

-- One Time Only (Implmentacion)
BEGIN
  SELECT j.job_id, j.function, count(e.employee_id)
  BULK COLLECT INTO v_tab
  FROM job j, employee e
  WHERE e.job_id = j.job_id
  GROUP BY j.job_id, j.function;
END;
```

